#version 460

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

#include "../utils.glsl"

layout (rgba32f, binding = 0) uniform image2D sourceImage;
layout (rgba32f, binding = 1) uniform image2D destinationImage;

uniform float intensity;
uniform float angle;

void main()
{
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
    vec2 texelf = vec2(texel);
    ivec2 size = ivec2(gl_NumWorkGroups.xy);
    vec2 sizef = vec2(size);

    vec4 originalColor = imageLoad(sourceImage, texel);
    vec4 newColor;

    vec2 direction = Rotated(vec2(1, 0), angle);
    direction.y *= -1.f;

    vec2 uvs = texelf / sizef;
    float offset = (0.005 * intensity);

    vec2 redOffset = uvs - direction * offset;
    vec2 blueOffset = uvs + direction * offset;

    newColor.r = imageLoad(sourceImage, ivec2(redOffset * sizef)).r;
    newColor.g = originalColor.g;
    newColor.b = imageLoad(sourceImage, ivec2(blueOffset * sizef)).b;
    newColor.a = originalColor.a;

    imageStore(destinationImage, texel, newColor);
}
